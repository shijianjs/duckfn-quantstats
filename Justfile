# 新扩展项目的 Justfile 模板 —— 从 duckfn 仓库的 templates/Justfile 复制过来。
#
# 复制后只改一处：把 extension_name 改成你的扩展名。它必须与
#   - src/extension/mod.rs 里 duckfn_entrypoint!("...") 的名字
#   - Makefile 里的 EXTENSION_NAME
#   - 构建产物 <extension_name>.duckdb_extension
# 三者一致，否则 LOAD 会失败。
#
# 前置工具：
#   cargo install just cargo-duckdb-ext-tools
#
# 说明：
#   - 日常迭代走 Cargo（just build / just sql / just repl），不需要 make 流程先跑通。
#   - sqllogictest 与 CI 走官方 makefile（just ci-init 一次，之后 just test）。

# Windows 下 recipe 交给 Git Bash 执行；按自己的 Git 安装路径调整。
# 非 Windows 上这一行不生效。
set windows-shell := ["C:\\Program Files\\Git\\bin\\bash.exe", "-c"]

# 扩展名：全小写、只含下划线
extension_name := "duckfn_quantstats"

# duckdb 命令行。不在 PATH 里时用 `just DUCKDB=/path/to/duckdb repl` 覆盖。
duckdb := env_var_or_default("DUCKDB", "duckdb")

ext_path := "./target/debug/" + extension_name + ".duckdb_extension"

# 不带参数运行 just 时列出所有 recipe
default:
    @just --list

# 日常构建 -> target/debug/<extension_name>.duckdb_extension
build:
    cargo duckdb-ext build

# 构建后跑一条 SQL 就退出：just sql "SELECT my_fn(1)"
sql sql: build
    {{duckdb}} -unsigned -c "LOAD '{{ext_path}}'; {{sql}}"

# 构建后进入 REPL（扩展已 LOAD），手动试函数用：just repl
repl: build
    {{duckdb}} -unsigned -cmd "LOAD '{{ext_path}}';"

# 全量 release 构建
release:
    cargo build --release

# 提交前检查，warning 视为错误
lint:
    cargo clippy --all-targets -- -D warnings

# 初始化 extension-ci-tools（生成 configure/ 与 python venv）；跑 make 流程前先来一次
ci-init:
    make configure

# 官方 debug 构建 —— sqllogictest 与 CI 走这条
ci-build: ci-init
    make debug

# 跑 test/sql/**/*.test
test: ci-build
    make test

# 官方 release 构建 —— CI 打 tag 时走这条
ci-release: ci-init
    make release

# WebAssembly 构建；要求 Makefile 的 [[example]] 名字与 extension_name 一致
build_wasm:
    cargo build --release --target wasm32-unknown-emscripten --example {{extension_name}}

# 工具链（首次）：固定 Rust 版本 + 装 wasm target
config_env:
    rustup override set 1.86.0
    rustup target add wasm32-unknown-emscripten
    rustup target list --installed
